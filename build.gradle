buildscript {
    ext {
        dependencyManagementVersion = '1.0.10.RELEASE'
        springBootVersion = '2.3.4.RELEASE'
        h2Version = '1.4.200'
        apacheCommonsLangVersion = '3.11'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'java'
    id 'io.spring.dependency-management' version "$dependencyManagementVersion"
}

allprojects {
    group = 'org.factory.dst'
    version "${version}"

    apply plugin: 'idea'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencyManagement {
        dependencies {
            dependencySet(group: 'org.springframework.boot', version: "${springBootVersion}") {
                entry 'spring-boot-starter-web'
                entry 'spring-boot-starter-data-jpa'
                entry 'spring-boot-starter-validation'
                entry 'spring-boot-starter-test'
            }
            dependency "com.h2database:h2:${h2Version}"
            dependency "org.apache.commons:commons-lang3:${apacheCommonsLangVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}
